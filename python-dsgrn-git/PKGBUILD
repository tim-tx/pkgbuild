_pkgname=python-dsgrn
pkgname=${_pkgname}-git
pkgver=0.1.r697.g2bd4373
pkgrel=1
pkgdesc="Dynamic Signatures Generated by Regulatory Networks"
arch=(any)
url="https://github.com/shaunharker/DSGRN"
license=('MIT')
depends=("python-mpi4py"
	 "python-pychomp"
	 "python-progressbar"
	 "python-scipy"
	 "python-numpy"
	 "python-matplotlib"
	 "python-graphviz"
	 "jupyter")
makedepends=("python" "python-setuptools")
provides=("${_pkgname}")
conflicts=("${_pkgname}")
source=("git://github.com/shaunharker/DSGRN.git")
sha256sums=('SKIP')

pkgver() {
    cd "${srcdir}/DSGRN"
    git describe --long | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
    cd "${srcdir}/DSGRN"
    git submodule update --init --recursive
    sed -i -e 's/jupyter/jupyter_core/' "${srcdir}/DSGRN/setup.py"
}

build() {
    cd "${srcdir}/DSGRN"
    python setup.py build
}

package() {
    cd "${srcdir}/DSGRN"
    python setup.py install --root="$pkgdir/" --optimize=1 --skip-build
    install -Dm644 LICENSE "$pkgdir/usr/share/licenses/${_pkgname}/LICENSE"
}
